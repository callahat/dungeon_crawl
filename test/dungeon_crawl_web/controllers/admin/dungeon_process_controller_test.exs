defmodule DungeonCrawlWeb.Admin.DungeonProcessControllerTest do
  use DungeonCrawlWeb.ConnCase
  use AssertEventually, timeout: 50, interval: 5

  alias DungeonCrawl.DungeonInstances
  alias DungeonCrawl.DungeonProcesses.DungeonRegistry

  describe "non registered users" do
    test "redirects non admin users", %{conn: conn} do
      conn = get conn, admin_dungeon_process_path(conn, :index)
      assert redirected_to(conn) == page_path(conn, :index)
    end
    # overkill to hit the other methods
  end

  describe "registered user but not admin" do
    setup [:normal_user]

    test "redirects non admin users", %{conn: conn} do
      conn = get conn, admin_dungeon_process_path(conn, :index)
      assert redirected_to(conn) == page_path(conn, :index)
    end
    # overkill to hit the other methods
  end

  describe "with an admin user" do
    setup [:admin_user]

    test "lists all entries on index", %{conn: conn} do
      instance = setup_dungeon_instance()
      conn = get conn, admin_dungeon_process_path(conn, :index)
      assert html_response(conn, 200) =~ "Listing dungeon processes"

      DungeonRegistry.remove(DungeonInstanceRegistry, instance.id)
    end

    test "shows chosen dungeon instance", %{conn: conn} do
      instance = setup_dungeon_instance()
      conn = get conn, admin_dungeon_process_path(conn, :show, instance.id)
      assert html_response(conn, 200) =~ "DB Backed Dungeon Process"

      DungeonRegistry.remove(DungeonInstanceRegistry, instance.id)
    end

    test "shows chosen dungeon instance when no backing db instance", %{conn: conn} do
      instance = setup_dungeon_instance()
      DungeonInstances.delete_dungeon(instance)
      conn = get conn, admin_dungeon_process_path(conn, :show, instance.id)
      assert html_response(conn, 200) =~ "Orphaned Dungeon Process"

      DungeonRegistry.remove(DungeonInstanceRegistry, instance.id)
    end

    test "redirects with a message when dungeon instance is nonexistent", %{conn: conn} do
      instance = setup_dungeon_instance()
      conn = get conn, admin_dungeon_process_path(conn, :show, -1)
      assert redirected_to(conn) == admin_dungeon_process_path(conn, :index)
      assert Flash.get(conn.assigns.flash, :info) == "Dungeon instance process not found: `-1`"

      DungeonRegistry.remove(DungeonInstanceRegistry, instance.id)
    end

    test "deletes chosen dungeon instance", %{conn: conn} do
      instance = setup_dungeon_instance()
      conn = delete conn, admin_dungeon_process_path(conn, :delete, instance.id)
      assert redirected_to(conn) == admin_dungeon_process_path(conn, :index)

      eventually assert DungeonInstances.get_dungeon(instance.id)
      eventually assert :error = DungeonRegistry.lookup(DungeonInstanceRegistry, instance.id)

      DungeonRegistry.remove(DungeonInstanceRegistry, instance.id)
    end
  end

  defp normal_user(_) do
    user = insert_user(%{username: "Threepwood", is_admin: false})
    conn = assign(build_conn(), :current_user, user)
    {:ok, conn: conn, user: user}
  end

  defp admin_user(_) do
    user = insert_user(%{username: "CSwaggins", is_admin: true})
    conn = assign(build_conn(), :current_user, user)
    {:ok, conn: conn, user: user}
  end

  defp setup_dungeon_instance() do
    di = insert_autogenerated_dungeon_instance()
    DungeonRegistry.create(DungeonInstanceRegistry, di.id)
    di
  end
end
